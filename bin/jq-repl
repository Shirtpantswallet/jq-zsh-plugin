#!/usr/bin/env zsh
#
# if 1st arg is '-' read from stdin
# if 1st arg is '--' take the command after it verbatim and execute it to get the input
# if 1st arg is anything else, treat it as a file
set -eu

JQ_REPL_JQ="${JQ_REPL_JQ:-jq}"
[[ $(print -rl -- ${(t)JQ_REPL_JQ}) =~ scalar* ]] && export JQ_REPL_JQ=( ${=JQ_REPL_JQ} )

if [ -n "${1:-}" ] && [ "$1" != "-" ] && [ "$1" != "--" ]; then
  input="$1"
  output=$(mktemp)
  trap 'rm -f "$output"' EXIT
else
  input=$(mktemp)
  output=$(mktemp)
  trap 'rm -f "$input" "$output"' EXIT
fi

if [ -z "${1:-}" ] || [ "$1" = "-" ]; then
  cat /dev/stdin >"$input"
fi
# Path to builtins file (same directory as this script)
builtins_file="${0:A:h}/jq-builtins"

if [ "${1:-}" = "--" ]; then
  shift
  export FZF_JQ_REPL_COMMAND="$* > $input; jq-paths <$input; $builtins_file 2>/dev/null"
else
  export FZF_JQ_REPL_COMMAND="jq-paths < \"$input\"; $builtins_file 2>/dev/null"
fi

# Use ASCII Unit Separator as FZF notation delimiter - extremely unlikely in any command
fzf_notation=$'\x1f'

# default ignore
FZF_COPY_TO_CLIPBOARD="execute-silent(ignore)"

# other platforms can be supported here as needed
command -v xclip > /dev/null && \
  FZF_COPY_TO_CLIPBOARD="execute-silent(echo -n {} | xclip -in -sel clip)+abort"

# TODO: add ability to modify jq args dynamically. This may have ripple effects on the output of `jq-paths`.
eval "$FZF_JQ_REPL_COMMAND" |
  fzf \
    --preview "jq-repl-preview {q} ${(q)input} ${(q)output}" \
    --preview-window="down:90%" \
    --height="99%" \
    --query="." \
    --bind "tab:replace-query,return:print-query" \
    --bind "ctrl-p:preview-up,ctrl-n:preview-down" \
    --bind "ctrl-alt-p:preview-half-page-up,ctrl-alt-n:preview-half-page-down" \
    --bind "shift-up:preview-up,shift-down:preview-down" \
    --bind "alt-up:preview-page-up,alt-down:preview-page-down" \
    --bind "ctrl-y:$FZF_COPY_TO_CLIPBOARD" \
    --bind "ctrl-r:reload${fzf_notation}${FZF_JQ_REPL_COMMAND}${fzf_notation}+refresh-preview"
